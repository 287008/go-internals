# 3.5 拷贝栈

Go语言支持goroutine，每个goroutine需要能够运行，所以它们都有自己的栈。假如每个goroutine分配固定栈大小并且不能增长，太小则会导致溢出，太大又会浪费空间，无法开许多的goroutine。为了解决这个问题，goroutine可以初始时只给栈分配很小的空间，然后随着使用过程中的需要自动地增长。这就是为什么Go可以开千千万万个goroutine而不会耗尽内存。

为了实现栈增长，Go1.3版本以下使用的是分裂栈技术，而1.3版本之后则使用的是拷贝栈。这里将具体分析一下Go语言的拷贝栈技术。

## 基本原理

每次执行函数调用时Go的runtime都会进行检测，当栈的大小不够用时，会触发“中断”，从当前函数进入到Go的运行时库，Go的运行时库会保存此时的函数上下文环境，然后分配一个新
的足够大的栈空间，将旧栈的内容拷贝到新栈中，并做一些设置，使得当函数恢复运行时，函数会在新分配的栈中继续执行，仿佛整个过程都没发生过一样，这个函数会觉得自己使用的
是一块大小“无限”的栈空间。

## 实现过程

在研究Go的实现细节之前让我们先自己思考一下应该如何实现。第一步肯定要有某种机制检测到当前栈大小不够用了，这个应该是把当前的栈寄存器SP跟栈的可用栈空间的边界进
行比较。能够检测到栈大小不够用，就相当于捕捉到了“中断”。

捕获完“中断”，第二步要做的，就应该是进入运行时，保存当前的上下文。别陷入如何保存上下文的细节，先假如我们把函数栈增长时的上下文保存好了，那下一步就是分配新的栈空间了，我们可以将分配空间想象成就是调用一下malloc而已

接下来怎么办呢？我们要将旧栈中的内容拷贝到新栈中，然后让函数继续在新栈中运行。这里先暂时忽略旧栈内容拷贝到新栈中的一些技术难点，假设在新栈空间中恢复了“中断”时
的上下文，从运行时返回到函数。

函数在新的栈中继续运行了，但是还有个问题：函数如何返回。因为函数返回后栈是要缩小的，否则就会内存浪费空间了，所以还需要在函数返回时处理栈缩小的问题。

## 具体细节

### 如何捕获函数的栈空间不足？

Go语言和C不同，不是使用栈指针寄存器和栈基址寄存器确定函数的栈的。在Go的运行时库中，每个goroutine对应一个结构体G。这个结构体中存了stackbase和stackguard，用于确定这个goroutine使用的栈空间信息。对于使用拷贝栈的函数，每个函数调用前几条指令，先比较栈指针寄存器跟g->stackguard，检测是否发生栈溢出。如果栈指针寄存器值超越了stackguard就需要扩展栈空间。

为了加深理解，下面让我们跟踪一下代码，并看看实际生成的汇编吧。首先写一个test.go文件，内容如下：

```go  
	package main

	func main() {
		main()
	}
```

然后生成汇编文件：

```sh
	go tool 6g -S test.go | head -8
```

可以看以输出是：

	000000 00000 (test.go:3)	TEXT	"".main+0(SB),$0-0
	000000 00000 (test.go:3)	MOVQ	(TLS),CX
	0x0009 00009 (test.go:3)	CMPQ	SP,(CX)
	0x000c 00012 (test.go:3)	JHI	,21
	0x000e 00014 (test.go:3)	CALL	,runtime.morestack00_noctxt(SB)
	0x0013 00019 (test.go:3)	JMP	,0
	0x0015 00021 (test.go:3)	NOP	,

让我们好好看一下这些指令。(TLS)取到的是结构体G的第一个域，也就是g->stackguard地址，将它赋值给CX。然后CX地址的值与SP进行比较，如果SP大于g->stackguard了，则会调用runtime.morestack00函数。这几条指令的作用就是检测栈是否溢出。

不过并不是所有函数在链接时都会插入这种指令。如果你读源代码，可能会发现"#pragma textflag 7"，或者在汇编函数中看到"TEXT reuntime.exit(SB),7,$0"，这种函数就是不会检测栈溢出的。这个是编译标记，控制是否生成栈溢出检测指令。

##

基于原栈SP的数据都可以直接复制到新栈中来。对于栈中存的是对象的地址，这样做不会造成问题。对于带参函数，因为参数不能直接搬，编译时要特殊处理．函数使用的参数指针不是基于栈帧的．对于在栈中返回对象的函数，对象必须返回到原栈上。
 
   扩展栈时，函数的返回地址会被修改成一个函数，这个函数会释放分配的栈块，将栈指针重新设置成调用者旧栈块的地址，栈指针等，需要在新栈空间中的某处保存着。
   
   


注意到它调用了runtime.morestack00。在asm_amd64.s中还有runtime.morestack10，runtime.morestack01，runtime.morestack11等好几个类似runtime.morestack00的函数，其实它们都是调用到runtime.morestack的。在stack.h中有说明为什么这么做，对不同大小的函数采用了不同的策略。每个goroutine的g->stackguard设置成指向栈底上面StackGuard的位置。每个函数会通过比较栈指针和g->stackguard检测栈溢出。对于那些只有很小栈帧的函数，为了减小检测的指令数目，可以允许栈越过stack guard下方StackSmall字节以内。使用较大栈的函数则总是会检测栈溢出并调用morestack。

	guard = g->stackguard
	frame = function's stack frame size
	argsize = size of function arguments (call + return)

	stack frame size <= StackSmall:
		CMPQ guard, SP
		JHI 3(PC)
		MOVQ m->morearg, $(argsize << 32)
		CALL morestack(SB)

	stack frame size > StackSmall but < StackBig
		LEAQ (frame-StackSmall)(SP), R0
		CMPQ guard, R0
		JHI 3(PC)
		MOVQ m->morearg, $(argsize << 32)
		CALL morestack(SB)

	stack frame size >= StackBig:
		MOVQ m->morearg, $((argsize << 32) | frame)
		CALL morestack(SB)

接着看一下runtime.morestack的实现。源代码在runtime/asm_amd64.s中。有两点信息要注意：第一点是morestack将一些信息存在struct M中，这些信息包括当前栈桢，参数，当前函数调用，函数返回地址，（两个返回地址，一个是runtime.morestack的函数地址，一个是f的返回地址）。通过这些信息可以把新栈和旧栈链起来。下面是具体的函数：

```asm
	TEXT runtime·morestack(SB),7,$0
		// 调度器的栈m->g0是无法增长的，下面几条指令是确保当前goroutine不是m->g0
		MOVQ	m_g0(BX), SI  //这里g0(BX)就是m->g0
		CMPQ	g(CX), SI  //g(CX)就是g
		JNE	2(PC)
		INT	$3  //INT $3是留给调试器的中断号，正常执行应该不会运行到这里

		MOVQ	DX, m_cret(BX) //BX是存的m结构体指针，m_cret(Bx)是m->cret，C的返回值。DX存的是

		// f是调用runtime.morestack的函数
		// 将m->morebuf设置成f的调用者
		MOVQ	8(SP), AX	// f's caller's PC
		MOVQ	AX, (m_morebuf+gobuf_pc)(BX) //将f的调用者的PC记录到m->morebuf.pc
		LEAQ	16(SP), AX	// f's caller's SP
		MOVQ	AX, (m_morebuf+gobuf_sp)(BX) //将f的调用者的SP记录到m->morebuf.sp
		MOVQ	AX, m_moreargp(BX) //morestack的参数
		get_tls(CX)
		MOVQ	g(CX), SI //当前的g -> SI
		MOVQ	SI, (m_morebuf+gobuf_g)(BX) //将当前的g记录到m->morebuf.g

		// Set m->morepc to f's PC.
		MOVQ	0(SP), AX 
		MOVQ	AX, m_morepc(BX) //将m->morepc设置为f的PC

		// Call newstack on m->g0's stack.
		// 切换到g0的栈，在g0的栈中调用newstack函数
		MOVQ	m_g0(BX), BP
		MOVQ	BP, g(CX) //将m->g0切换成当前g
		MOVQ	(g_sched+gobuf_sp)(BP), SP //将m->g0->g_sched.gobuf_sp设置成当前的SP
		CALL	runtime·newstack(SB) //调用newstack
		MOVQ	$0, 0x1003	// crash if newstack returns
		RET
```

如果将上面的汇编转换为类似的C代码，将是下面这样的：

```C
	void runtime.morestack() {
		if(g == g0) {
			panic();
		} else {
			m->morebuf.gobuf_pc = getCallerCallerPC();
			void *SP = getCallerSP();
			m->morebuf.gobuf_sp = SP;
			m->moreargp = SP;
			m->morebuf.gobuf_g = g;
			m->morepc = getCallerPC();

			void *g0 = m->g0;
			g = g0;
			setSP(g0->g_sched.gobuf_sp);
			runtime.newstack();
		}
	}
```

这段源代码中有函数f和函数f的调用者，要注意将两者区分开来。函数f指的是调用morestack的函数，但不是前面的morestack00，morestack01等，而是真正触发栈增长的函数。虽然控制流是从f到morestack00再到morestack的，但是morestack00到morestack的方式并不是调用的CALL，而是直接JMP过来的。所以0(SP)是保存的f的返回地址。为什么8(SP)会是f的调用者的返回地址呢？因为进入函数后最前几条指令就是检测栈溢出的，它还没来得及使用自己的栈空间，它的栈是空的，所以此时8(SP)就是f的调用者的返回地址，是CALL指令时推进来的。

然后需要注意的就是newstack是切换到m->g0的栈中去调用的。m->g0是调度器栈，go的运行时库调度器使用的都是m->g0。

newstack中有很大一部分跟reflect相关的代码，现在先不管它。newstack做的事情很容易看明白：分配新栈空间，在该空间顶部初始化一个struct Stktop的东西，然后以很奇特的方式退出。

```go
	//分配新空间
	stk = runtime·stackalloc(framesize);
	top = (Stktop*)(stk+framesize-sizeof(*top)); 
	free = framesize;
	//将一些重要信息从结构体g中移到栈顶Stktop中
	top->stackbase = (byte*)gp->stackbase;
	top->stackguard = (byte*)gp->stackguard; 
	top->gobuf = m->morebuf; //只是借助m结构体临时传了一下参数，morebuf中记录的是栈空间不够的那个函数的pc,sp,g
	top->argp = m->moreargp; //参数
	top->argsize = argsize; //参数大小
	top->free = free; //可用空间大小
	//装饰栈顶的参数区域
	sp = (byte*)top; //top结构体中存的是一些重要信息，结构体下方就是可用的栈空间起始位置了。
	if(argsize > 0) { //将参数移过来
	sp -= argsize;
	dst = (uintptr*)sp;
	dstend = dst + argsize/sizeof(*dst);
	src = (uintptr*)top->argp;
	while(dst < dstend)
	*dst++ = *src++;
	}
	//跳转，伪装成好像是从m->morepc中调用lessstack函数的状态
	label.sp = (uintptr)sp;
	label.pc = (byte*)runtime·lessstack;
	label.g = m->curg;
	runtime·gogocall(&label, m->morepc, m->cret); //gogocall相当于一个直接的jmp，不是按函数协议跳转的
```

要稍微注意看的是装饰栈顶的部分代码，此时的内存布局，上方是一个Stktop结构体，下方就是栈，并且参数已经进栈了。接下来调用gogocall，这个函数的三个参数分别是Gobuf，一个函数地址，以及一个返回值。该函数会跳转到第二个参数标识的函数中，但返回时会返回到Gobuf保存的上下文，而不是调用gogocall的函数。

```asm
	// void gogocall(Gobuf*, void (*fn)(void), uintptr r0)
	// 跳转到fn，从fn返回时会返回到Gobuf保存的状态
	TEXT runtime·gogocall(SB), 7, $0
	MOVQ	24(SP), DX	// gogocall函数的第3个参数，实际上是m->cret
	MOVQ	16(SP), AX		// 第2个参数，fn
	MOVQ	8(SP), BX		// 第1个参数，是一个gobuf结构体指针
	MOVQ	gobuf_g(BX), DI	//gobuf->g
	get_tls(CX)
	MOVQ	DI, g(CX)	//设置回之前的g...现在这个函数是在m->g0中调用的
	MOVQ	0(DI), CX	// make sure g != nil
	MOVQ	gobuf_sp(BX), SP	// 这下子SP才真正的切换到新栈中了
	MOVQ	gobuf_pc(BX), BX
	PUSHQ	BX	// 在刚进入函数时是SP指向保存的返回地址的。这里正好是这样，当函数返回时就会返回到BX了，也就是lessstack
	JMP	AX
	POPQ	BX	// not reached
```

runtime.lessstack比较简单，它其实就是切换到m->g0栈之后调用runtime.oldstack函数。这时之前保存的那个Stktop结构体是时候发挥作用了，从上面可以找到旧栈空间的SP和PC等信息，通过runtime.gogo跳转过去，整个过程就完成了。

```go
	gp = m->curg; //当前g
	top = (Stktop*)gp->stackbase; //取得Stktop结构体
	label = top->gobuf; //从结构体中取出Gobuf
	runtime·gogo(&label, cret); //通过Gobuf恢复上下文
```

整个分裂栈的细节终于分析完了，函数调用链是f->morestack00->morestack->newstack->f在新栈中->f调用的函数...->lessstack->f。

再重复一遍整个过程：

栈帧的全局缓存，用于阻止每个线程栈的不受限增长

newstack 分配一个足够大的栈(大于m->moreframesize)，将m->moreframesize字节拷贝到新的栈中，然后伪装runtime.lessstack刚刚掉用过m->morepc



## 小结

1. 使用分段栈的函数头几个指令检测%esp和stackguard，调用于runtime.morestack
2. runtime.more函数的主要功能是保存当前的栈的一些信息，然后转换成调度器的栈了调用runtime.newstack
3. runtime.newstack函数的主要功能是分配空间，装饰此空间，将旧的frame和arg弄到新空间
4. 使用gogocall的方式切换到新分配的栈，gogocall使用的JMP返回到被中断的函数
5. 继续执行遇到RET指令时会返回到runtime.less，less做的事情跟more相反，它要准备好从newstack到old　stack
  整个过程有点像一次中断，中断处理时保存当时的现场，弄个新的栈，中断恢复时恢复到新栈中运行，运行到return时又要从runtime.less走回去

## links
 * [目录](<preface.md>)
 * 上一节: [defer关键字](<03.4.md>)
 * 下一节: [方法调用](<03.6.md>)
